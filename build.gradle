plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
}

java {
  sourceCompatibility = '21'
  targetCompatibility = '21'
}

repositories {
  mavenCentral()
}

dependencies {

  //dependencies for productive code
  implementation 'com.microsoft.sqlserver:mssql-jdbc:12.6.1.jre11'
  implementation 'io.netty:netty-all:4.1.90.Final'

  //dependencies for test code
  testImplementation 'net.bytebuddy:byte-buddy:1.14.11'
  testImplementation 'net.bytebuddy:byte-buddy-agent:1.14.11'
  testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.2'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.2'
  testImplementation 'org.mockito:mockito-core:5.8.0'
}

sourceSets {

  main {

    java {
      srcDirs = ['Source']
    }

    resources {
      srcDirs = ['Resource']
    }
  }

  test {
    java {
      srcDirs = ['Test', 'PerformanceTest']
    }
  }
}

group = 'ch.nolix'
version = '0.0.0'

signing {
  sign publishing.publications
}

publishing {

  publications {
    nolix(MavenPublication) {

      from components.java

      pom {

        name = 'Nolix'
        description = 'Nolix provides a smart web framework, an ORM (object relation mapper) and some technical functions.'
        url = 'https://github.com/silvanwyss/nolix'

        licenses {
          license {
            name = 'MIT License'
            url = 'https://opensource.org/license/mit'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'SilvanWyss'
            name = 'Silvan Wyss'
            email = 'silvan.wyss@gmail.com'
          }
        }
      }
    }
  }

  repositories {
    maven {

      url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'

      credentials {
        username = "${maven_username}"
        password = "${maven_password}"
      }
    }
  }
}
