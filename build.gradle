plugins {
  id 'java-library'
  id 'maven-publish'
  id 'signing'
}

java {
  sourceCompatibility = '25'
  targetCompatibility = '25'
}

repositories {
  mavenCentral()
}

dependencies {

  //dependencies for implementations
  implementation 'com.microsoft.sqlserver:mssql-jdbc:12.8.1.jre11'
  implementation 'io.netty:netty-all:4.1.115.Final'

  //dependencies for tests
  testImplementation 'com.tngtech.archunit:archunit:1.4.1'
  testImplementation 'net.bytebuddy:byte-buddy:1.17.6'
  testImplementation 'net.bytebuddy:byte-buddy-agent:1.17.6'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-params:5.13.1'
  testImplementation 'org.junit.platform:junit-platform-runner:1.13.2'
  testImplementation 'org.mockito:mockito-core:5.18.0'
}

sourceSets {

  main {

    java {
      srcDirs = ['Source']
    }

    resources {
      srcDirs = ['Resource']
    }
  }

  test {
    java {
      srcDirs = ['Test', 'ArchitectureTest', 'PerformanceTest']
    }
  }
}

test {
    useJUnitPlatform()
}

group = 'ch.nolix'
version = '0.0.0'

signing {
  sign publishing.publications
}

publishing {

  publications {
    nolix(MavenPublication) {

      from components.java

      pom {

        name = 'Nolix'
        description = 'Nolix provides a smart web framework, an ORM (object relation mapper) and some technical functions.'
        url = 'https://github.com/silvanwyss/nolix'

        licenses {
          license {
            name = 'MIT License'
            url = 'https://opensource.org/license/mit'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'SilvanWyss'
            name = 'Silvan Wyss'
            email = 'silvan.wyss@gmail.com'
          }
        }
      }
    }
  }

  repositories {
    maven {

      url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'

      credentials {
        username = "${maven_username}"
        password = "${maven_password}"
      }
    }
  }
}
